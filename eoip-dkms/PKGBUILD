# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

_pkgbase=eoip
_eoip_kernel_ver=5.6.0
_commit=085c7c4e1c0e50d90b7d90f61a12e12b317a91e2
_srcname=linux-${_commit}

pkgname=${_pkgbase}-dkms
pkgver=0.6
pkgrel=1
pkgdesc='Kernel mode EOIP (Ethernet Over IP) tunnel compatible with MikroTik RouterOS'
arch=('any')
url='http://github.com/bbonev/eoip'
license=('GPL')
depends=('dkms')
optdepends=("${_pkgbase}-utils")
source=("${_pkgbase}-${pkgver}.tar.gz::http://github.com/bbonev/eoip/archive/refs/tags/v${pkgver}.tar.gz"
        "linux-${_commit}.tar.gz::http://github.com/torvalds/linux/archive/${_commit}.tar.gz"
	'Makefile'
	'dkms.conf'
	'0001-EoIP-Handle-inbound-traffic-when-no-source-address-i.patch')
sha256sums=('b4ebbbbbd37b5e957ac237188e672e20c555a6c7e85f1725e48e8033e08a7fc3'
            '6ece311a89ca3263afa4613d2911eeae37332d3237af7f4ab87cdc147faab8eb'
            'bf860f6823f1dc9d43b67df75273a0d9b5130c675f28cf98a89569d391973ef0'
            '20a9544db679d645c235eeefab550d88ae52db2bacb17d9862518cf74852746e'
            '2fb2a32367f55d281c3b35b3fbf2e70ac6205c7b26f058bde3ce4ba218a42f1a')

prepare() {
	cd "${srcdir}/linux-${_commit}"
	for p in eoip-gre-demux eoip; do
		patch -p1 < ${srcdir}/${_pkgbase}-${pkgver}/kernel-patch/kernel-${_eoip_kernel_ver}-$p.patch
	done
	git apply ${srcdir}/0001-EoIP-Handle-inbound-traffic-when-no-source-address-i.patch
}

package() {
        local _dkms_src="${pkgdir}/usr/src/${_pkgbase}-${pkgver}"

	install -Dm644 ${srcdir}/dkms.conf ${_dkms_src}/dkms.conf

	# Set name and version
	sed -e "s/@PKGBASE@/${_pkgbase}/" \
	  -e "s/@PKGVER@/${pkgver}/" \
	  -i "${_dkms_src}/dkms.conf"

	
	install -Dm644 ${srcdir}/Makefile ${_dkms_src}/Makefile
	install -Dm644 ${srcdir}/linux-${_commit}/net/ipv4/eoip.c ${_dkms_src}/eoip.c
	install -Dm644 ${srcdir}/linux-${_commit}/net/ipv4/gre_demux.c ${_dkms_src}/gre.c
}

# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

_pkgbase=eoip
_eoip_kernel_ver=5.6.0

pkgname=${_pkgbase}-utils
pkgver=0.6
pkgrel=1
pkgdesc='Kernel mode EOIP tunnel compatible with MikroTik RouterOS - Management utilities'
arch=('i686' 'x86_64' 'aarch64' 'armv7h' 'armv6h')
url='http://github.com/bbonev/eoip'
license=('GPL')
optdepends=("${_pkgbase}-dkms")
makedepends=('git')
source=(
	"${_pkgbase}-${pkgver}.tar.gz::http://github.com/bbonev/eoip/archive/refs/tags/v${pkgver}.tar.gz"
	'0001-Simplify-Makefile-for-eoip-utility.patch'
)
sha256sums=('b4ebbbbbd37b5e957ac237188e672e20c555a6c7e85f1725e48e8033e08a7fc3'
            'fc2c05547c792d19bfc10d925309e1171c9138f0ffef1f115bf45cad348edcae')

prepare() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	for f in "${source[@]}"; do
		if [[ $f =~ \.patch$ ]]; then
			msg2 "Applying ${f##*/}"
			git apply "$srcdir/${f##*/}"
		fi
	done
}

build() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	make
}

package() {
	install -Dsm755 ${srcdir}/${_pkgbase}-${pkgver}/eoip  ${pkgdir}/usr/bin/eoip
}

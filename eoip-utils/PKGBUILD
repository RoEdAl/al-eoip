# Maintainer: Edmunt Pienkowsky <roed@onet.eu>

_pkgbase=eoip
_eoip_kernel_ver=5.6.0

pkgname=${_pkgbase}-utils
pkgver=0.6
pkgrel=1
pkgdesc='Kernel mode EOIP tunnel compatible with MikroTik RouterOS - Management utilities'
arch=('i686' 'x86_64' 'aarch64' 'armv7h' 'armv6h')
url='http://github.com/bbonev/eoip'
license=('GPL')
optdepends=("${_pkgbase}-dkms")
makedepends=('git')
source=(
	"${_pkgbase}-${pkgver}.tar.gz::http://github.com/bbonev/eoip/archive/refs/tags/v${pkgver}.tar.gz"
	'0001-Simplify-Makefile-for-eoip-utility.patch'
	'eoip-iface.sh'
	'eoip@.service'
	'25-eoip.link'
	'50-eoip.network'
	'env'
)
sha256sums=('b4ebbbbbd37b5e957ac237188e672e20c555a6c7e85f1725e48e8033e08a7fc3'
            'd52a73ae47ed1cfbbe42595a53fdd7b50abd5b289a1b58de4077b9c28eb85011'
            '197bfc3ec5587a81046d0c6bd655a4ebfd3149cfdcf9233b3d564cd9ecb130c8'
            'f6ff2d204ffae48316fe355900b8bd3d21557e21c1d569b022da70813770d93c'
            '4c900d7272f6210eae732769fa086067307813517ff08a03168144057d1c5afb'
            '92a47fc0e51d6f9eacfa29bcff2e77a47168ce464438c997a96781ce5a0f963a'
            '15a7555274e2e7ac4fc98750a6c65b104ed1e521f761bd7395909d1e2b7ae594')

backup=("etc/conf.d/${_pkgbase}")

prepare() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	for f in "${source[@]}"; do
		if [[ $f =~ \.patch$ ]]; then
			msg2 "Applying ${f##*/}"
			git apply "$srcdir/${f##*/}"
		fi
	done
}

build() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	make
}

package() {
	install -d -m 0755 ${pkgdir}/usr/bin
	install -p -m 0755 ${srcdir}/${_pkgbase}-${pkgver}/eoip ${pkgdir}/usr/bin/eoip

	install -d -m 0755 ${pkgdir}/usr/lib/systemd/system
	install -p -m 0644 ${srcdir}/eoip@.service ${pkgdir}/usr/lib/systemd/system/${_pkgbase}@.service

	install -d -m 0755 ${pkgdir}/usr/lib/${_pkgbase}
	install -p -m 0755 ${srcdir}/eoip-iface.sh ${pkgdir}/usr/lib/${_pkgbase}/${_pkgbase}-iface.sh

	install -d -m 0755 ${pkgdir}/etc/conf.d
	install -p -m 0644 ${srcdir}/env ${pkgdir}/etc/conf.d/${_pkgbase}

	install -d -m 0755 ${pkgdir}/usr/lib/systemd/network
	install -p -m 0644 ${srcdir}/25-eoip.link ${pkgdir}/usr/lib/systemd/network/25-${_pkgbase}.link
	install -p -m 0644 ${srcdir}/50-eoip.network ${pkgdir}/usr/lib/systemd/network/50-${_pkgbase}.network
}
